plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.moowork.node' version '1.3.1'
    id 'java'
    id 'jacoco'
}

group = 'underdogs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

node {
    version = '12.16.2'
    npmVersion = '6.14.4'
    workDir = file('./src/frontend')
    npmWorkDir = file('./src/frontend')
    nodeModulesDir = file('./src/frontend')
}

task setUp(type: NpmTask) {
    description = 'Install Node.js packages'
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task buildFrontend(type: NpmTask, dependsOn: setUp) {
    description = 'Build vue.js'
    args = ['run', 'build']
}

processResources.dependsOn 'buildFrontend'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled true
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
}